"ARM relaxing ppo to accomodate qualcomm behaviors"

include "cos.cat"

(* Uniproc *)
let poi = po-loc
let complus = fr|rf|co|(co;rf)|(fr;rf)
irreflexive poi;complus as uniproc

(* Utilities *)
let dd = addr | data
let rdw = po-loc & (fre;rfe)
let detour = po-loc & (coe ; rfe)
let addrpo = addr;po
(*******)
(* ppo *)
(*******)


(* Initial value *)
let ci0 = ctrlisync | detour
let ii0 = dd | rfi | rdw
let cc0 = dd | ctrl | addrpo |
  ( po-loc \ ( rfi | (po-loc;rfi)))   (* po-loc replaced *)
let ic0 = 0

include "ppo.cat"

(**********)
(* fences *)
(**********)

let WW = W * W
and RM = R * M

(* Power *)
let lwsync = lwsync & RM | lwsync & WW
let eieio = eieio & WW
show sync, lwsync, eieio

(* ARM *)
let dmb.st=dmb.st & WW
let dsb.st=dsb.st & WW
show dmb, dsb, dmb.st, dsb.st

(* Common, all arm barriers are strong *)
let strong = sync|dmb|dsb|dmb.st|dsb.st
let light = lwsync|eieio

let fence = strong|light


(* extensions *)
let ppoext = (rfe;ppo)|(ppo;rfe)|(rfe;ppo;rfe)
let fenceext = (rfe;fence)|(fence;rfe)|(rfe;fence;rfe)

(* happens before *)
let hb = ppo | ppoext | fence | fenceext
let GHB = hb
acyclic hb as thinair

(* prop *)
let hbstar = hb*
let propbase = (fence|fenceext);hbstar

let chapo = rfe|fre|coe|(fre;rfe)|(coe;rfe)

let prop = (propbase & WW)| (chapo? ; propbase*; strong; hbstar)

acyclic co|prop as propagation
irreflexive fre;prop;hbstar as causality

