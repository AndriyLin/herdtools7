"Model pretty print"
(* Same as model, shows only basic relations *)
include "filters.cat"
include "cos.cat"

(* Uniproc *)
acyclic po-loc | rf | fr | co as uniproc

(* Utilities *)
let dd = addr | data
let rdw = po-loc & (fre;rfe)
let detour = po-loc & (coe ; rfe)
let addrpo = addr;po

(*******)
(* ppo *)
(*******)

include "armfences.cat"
include "ppcfences.cat"
show isync, isb, ctrlisync,ctrlisb

(* Initial value *)
let ci0 = ctrlisync| ctrlisb | detour
let ii0 = dd | rfi | rdw
let cc0 = dd | po-loc | ctrl | addrpo
let ic0 = 0

include "ppo.cat"


(**********)
(* fences *)
(**********)

(* Power *)
let lwsync_eff = RM(lwsync)|WW(lwsync)
let eieio_eff = WW(eieio)
show sync, lwsync, eieio

(* ARM *)
let dmb.st_eff=WW(dmb.st)
let dsb.st_eff=WW(dsb.st)
show dmb, dsb, dmb.st, dsb.st

(* Common, all arm barriers are strong *)
let strong = sync|dmb|dsb|dmb.st_eff|dsb.st_eff
let light = lwsync_eff|eieio_eff

include "ppc-checks.cat"
